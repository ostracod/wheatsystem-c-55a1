
INCLUDE "../macros.wbasm"
INCLUDE "../utils.wbasm"
INCLUDE "../stringBuilder.wbasm"

MACRO readEnemyTable, dest, index
    add offset, entryOffset, index
    wrt startOffset, appData[offset]:s32
    add offset, prevArg, 4
    wrt endOffset, appData[offset]:s32
    sub size, endOffset, startOffset
    newAlloc dest, guardedAllocAttr, size
    wrtBuff dest[0]:s8, appData[startOffset]:s8, size
END

DEF maxHealthScale, 5
DEF levelExperienceScale, 2
DEF maxEnergy, 10
DEF enemyEntrySize, 16
DEF kickboxEnergy, 2

VAR shellHandle, s32
VAR utilsHandle, s32
VAR displayMessageIndex, s32
VAR promptOptionIndex, s32
VAR randomIntIndex, s32
VAR window, s32

VAR restMessage, s32
VAR lowEnergyMessage, s32
VAR helpMessage, s32
VAR helplessMessage, s32
VAR catMessage, s32
VAR dogMessage, s32
VAR stalactiteMessage, s32
VAR travelOptions, s32
VAR battleOptions, s32

VAR playerLevel, s8
VAR playerExperience, s16
VAR playerHealth, s16
VAR playerEnergy, s16
VAR playerPos, s8
VAR enemySpeciesAmount, s8
VAR enemySpecies, s8
VAR enemySpeciesName, s32
VAR enemyAttackName, s32
VAR enemyLevel, s8
VAR enemyHealth, s16

FUNC init, wsInit.id, guarded
    VAR shellName, s32
    newTextAlloc shellName, shellNameStart, shellNameEnd
    openFile shellHandle, shellName
    delAlloc shellName
    findFunc displayMessageIndex, shellHandle, wsDisplayWindowMessage.id
    findFunc promptOptionIndex, shellHandle, wsPromptWindowOption.id
    
    VAR utilsName, s32
    newTextAlloc utilsName, utilsNameStart, utilsNameEnd
    openFile utilsHandle, utilsName
    delAlloc utilsName
    launch utilsHandle
    initStringBuilderVars
    findFunc randomIntIndex, utilsHandle, generateRandomInt.id
    
    newTextAlloc restMessage, restMessageStart, restMessageEnd
    newTextAlloc lowEnergyMessage, lowEnergyMessageStart, lowEnergyMessageEnd
    newTextAlloc helpMessage, helpMessageStart, helpMessageEnd
    newTextAlloc helplessMessage, helplessMessageStart, helplessMessageEnd
    newTextAlloc catMessage, catMessageStart, catMessageEnd
    newTextAlloc dogMessage, dogMessageStart, dogMessageEnd
    newTextAlloc stalactiteMessage, stalactiteMessageStart, stalactiteMessageEnd
    
    VAR text, s32
    
    newAlloc travelOptions, guardedAllocAttr, 12
    newTextAlloc text, restOptionStart, restOptionEnd
    wrt travelOptions[0]:s32, text
    newTextAlloc text, forwardOptionStart, forwardOptionEnd
    wrt travelOptions[4]:s32, text
    newTextAlloc text, backwardOptionStart, backwardOptionEnd
    wrt travelOptions[8]:s32, text
    
    newAlloc battleOptions, guardedAllocAttr, 12
    newTextAlloc text, punchOptionStart, punchOptionEnd
    wrt battleOptions[0]:s32, text
    newTextAlloc text, kickboxOptionStart, kickboxOptionEnd
    wrt battleOptions[4]:s32, text
    newTextAlloc text, helpOptionStart, helpOptionEnd
    wrt battleOptions[8]:s32, text
    
    VAR newWindowIndex, s32
    findFunc newWindowIndex, shellHandle, wsNewWindow.id
    newArgFrame wsNewWindow.argsSize
    callRemote shellHandle, newWindowIndex
    wrt window, wsNewWindow.args.dest
    
    VAR promptSeedIndex, s32
    findFunc promptSeedIndex, utilsHandle, promptRandomSeed.id
    newArgFrame promptRandomSeed.argsSize
    wrt promptRandomSeed.args.window, window
    callRemote utilsHandle, promptSeedIndex
    
    VAR tableSize, s16
    sub tableSize, ?enemyTableEnd, ?enemyTableStart
    div enemySpeciesAmount, tableSize, enemyEntrySize
    
    LBL runLoop
    call runGame
    jmp runLoop
END

FUNC runGame
    VAR message, s32
    
    wrt playerLevel, 5
    wrt playerExperience, 0
    wrt playerEnergy, maxEnergy
    wrt playerPos, 0
    
    newTextAlloc message, introMessageStart, introMessageEnd
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    LBL gameLoop
    mul playerHealth, playerLevel, maxHealthScale
    call performTravel
    jmpG handleCoward, 0, playerPos
    jmpNG handleVictory, 10, playerPos
    call performBattle
    jmpNG handleDeath, playerHealth, 0
    jmp gameLoop
    
    LBL handleCoward
    newTextAlloc message, cowardMessageStart, cowardMessageEnd
    jmp displayMessage
    LBL handleDeath
    newTextAlloc message, deathMessageStart, deathMessageEnd
    jmp displayMessage
    LBL handleVictory
    newTextAlloc message, victoryMessageStart, victoryMessageEnd
    LBL displayMessage
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
END

FUNC performTravel
    VAR nextLevelExperience, s16
    VAR posInMeters, s16
    VAR stringBuilder, s32
    VAR message, s32
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?levelLabelStart, ?levelLabelEnd
    addIntToString stringBuilder, playerLevel
    addAppDataToString stringBuilder, ?playerNameStart, ?playerNameEnd
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?experienceLabelStart, ?experienceLabelEnd
    addIntToString stringBuilder, playerExperience
    addCharToString stringBuilder, 47
    mul nextLevelExperience, playerLevel, levelExperienceScale
    addIntToString stringBuilder, nextLevelExperience
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?energyLabelStart, ?energyLabelEnd
    addIntToString stringBuilder, playerEnergy
    addCharToString stringBuilder, 47
    addIntToString stringBuilder, maxEnergy
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?posLabelStart, ?posLabelEnd
    mul posInMeters, playerPos, 10
    addIntToString stringBuilder, posInMeters
    addCharToString stringBuilder, 109
    finishString message, stringBuilder
    
    LBL promptLoop
    VAR index, s8
    newArgFrame wsPromptWindowOption.argsSize
    wrt wsPromptWindowOption.args.window, window
    wrt wsPromptWindowOption.args.message, message
    wrt wsPromptWindowOption.args.options, travelOptions
    callRemote shellHandle, promptOptionIndex
    wrt index, wsPromptWindowOption.args.dest
    jmpE handleRest, index, 0
    jmpE handleForward, index, 1
    jmpE handleBackward, index, 2
    jmp promptLoop
    
    LBL handleRest
    add playerEnergy, prevArg, 5
    jmpNG skipClampEnergy, playerEnergy, maxEnergy
    wrt playerEnergy, maxEnergy
    LBL skipClampEnergy
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, restMessage
    callRemote shellHandle, displayMessageIndex
    jmp cleanUp
    
    LBL handleForward
    inc playerPos
    jmp cleanUp
    
    LBL handleBackward
    dec playerPos
    
    LBL cleanUp
    delAlloc message
END

FUNC performBattle
    newArgFrame generateRandomInt.argsSize
    wrt generateRandomInt.args.maximumValue, enemySpeciesAmount
    callRemote utilsHandle, randomIntIndex
    wrt enemySpecies, generateRandomInt.args.dest
    
    newArgFrame generateRandomInt.argsSize
    wrt generateRandomInt.args.maximumValue, 3
    callRemote utilsHandle, randomIntIndex
    add enemyLevel, playerPos, generateRandomInt.args.dest
    add enemyLevel, prevArg, 2
    mul enemyHealth, enemyLevel, maxHealthScale
    
    VAR entryOffset, s32
    VAR offset, s32
    VAR startOffset, s32
    VAR endOffset, s32
    VAR size, s8
    mul entryOffset, enemySpecies, enemyEntrySize
    add entryOffset, prevArg, ?enemyTableStart
    readEnemyTable enemySpeciesName, 0
    readEnemyTable enemyAttackName, 8
    
    VAR stringBuilder, s32
    VAR message, s32
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?wildMessageStart, ?wildMessageEnd
    addTextToString stringBuilder, enemySpeciesName
    addAppDataToString stringBuilder, ?appearedMessageStart, ?appearedMessageEnd
    finishString message, stringBuilder
    
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    LBL turnLoop
    call performBattleTurn
    jmpNG handleVictory, enemyHealth, 0
    jmpNG cleanUp, playerHealth, 0
    jmp turnLoop
    
    LBL handleVictory
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?defeatEnemyMessageStart, ?defeatEnemyMessageEnd
    addTextToString stringBuilder, enemySpeciesName
    addCharToString stringBuilder, 33
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    VAR experienceReward, s16
    sub experienceReward, enemyLevel, playerLevel
    add experienceReward, prevArg, 3
    jmpG skipClamp, experienceReward, 0
    wrt experienceReward, 1
    LBL skipClamp
    add playerExperience, prevArg, experienceReward
    
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?earnedMessageStart, ?earnedMessageEnd
    addIntToString stringBuilder, experienceReward
    addAppDataToString stringBuilder, ?experienceMessageStart, ?experienceMessageEnd
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    VAR hasLeveledUp, s8
    VAR nextLevelExperience, s16
    wrt hasLeveledUp, 0
    LBL levelUpLoop
    mul nextLevelExperience, playerLevel, levelExperienceScale
    jmpG breakLevelUp, nextLevelExperience, playerExperience
    inc playerLevel
    sub playerExperience, prevArg, nextLevelExperience
    wrt hasLeveledUp, 1
    jmp levelUpLoop
    LBL breakLevelUp
    jmpZ cleanUp, hasLeveledUp
    
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?levelUpMessageStart, ?levelUpMessageEnd
    addIntToString stringBuilder, playerLevel
    addCharToString stringBuilder, 33
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    LBL cleanUp
    delAlloc enemySpeciesName
    delAlloc enemyAttackName
END

FUNC performBattleTurn
    VAR stringBuilder, s32
    VAR message, s32
    VAR promptMessage, s32
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?levelLabelStart, ?levelLabelEnd
    addIntToString stringBuilder, playerLevel
    addAppDataToString stringBuilder, ?playerNameStart, ?playerNameEnd
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?healthLabelStart, ?healthLabelEnd
    addIntToString stringBuilder, playerHealth
    addCharToString stringBuilder, 32
    addAppDataToString stringBuilder, ?energyLabelStart, ?energyLabelEnd
    addIntToString stringBuilder, playerEnergy
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?levelLabelStart, ?levelLabelEnd
    addIntToString stringBuilder, enemyLevel
    addCharToString stringBuilder, 32
    addTextToString stringBuilder, enemySpeciesName
    addCharToString stringBuilder, 10
    addAppDataToString stringBuilder, ?healthLabelStart, ?healthLabelEnd
    addIntToString stringBuilder, enemyHealth
    finishString promptMessage, stringBuilder
    
    LBL promptLoop
    VAR index, s8
    newArgFrame wsPromptWindowOption.argsSize
    wrt wsPromptWindowOption.args.window, window
    wrt wsPromptWindowOption.args.message, promptMessage
    wrt wsPromptWindowOption.args.options, battleOptions
    callRemote shellHandle, promptOptionIndex
    wrt index, wsPromptWindowOption.args.dest
    jmpE handlePunch, index, 0
    jmpE handleKickbox, index, 1
    jmpE handleHelp, index, 2
    jmp promptLoop
    
    VAR damageAmount, s16
    LBL handlePunch
    wrt damageAmount, playerLevel
    jmp damageEnemy
    LBL handleKickbox
    jmpG handleLowEnergy, kickboxEnergy, playerEnergy
    mul damageAmount, playerLevel, 2
    sub playerEnergy, prevArg, kickboxEnergy
    
    LBL damageEnemy
    newArgFrame generateRandomInt.argsSize
    wrt generateRandomInt.args.maximumValue, 3
    callRemote utilsHandle, randomIntIndex
    add damageAmount, prevArg, generateRandomInt.args.dest
    sub enemyHealth, prevArg, damageAmount
    jmpNG skipClampEnemy, 0, enemyHealth
    wrt enemyHealth, 0
    LBL skipClampEnemy
    
    createStringBuilder stringBuilder
    addTextToString stringBuilder, enemySpeciesName
    addAppDataToString stringBuilder, ?lostMessageStart, ?lostMessageEnd
    addIntToString stringBuilder, damageAmount
    addAppDataToString stringBuilder, ?healthMessageStart, ?healthMessageEnd
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    jmp enemyAttack
    
    LBL handleLowEnergy
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, lowEnergyMessage
    callRemote shellHandle, displayMessageIndex
    jmp promptLoop
    
    LBL handleHelp
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, helpMessage
    callRemote shellHandle, displayMessageIndex
    
    newArgFrame generateRandomInt.argsSize
    wrt generateRandomInt.args.maximumValue, 6
    callRemote utilsHandle, randomIntIndex
    wrt index, generateRandomInt.args.dest
    jmpE handleCat, index, 0
    jmpE handleDog, index, 1
    jmpE handleStalactite, index, 2
    
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, helplessMessage
    callRemote shellHandle, displayMessageIndex
    jmp enemyAttack
    
    LBL handleCat
    wrt playerEnergy, maxEnergy
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, catMessage
    callRemote shellHandle, displayMessageIndex
    jmp enemyAttack
    
    LBL handleDog
    mul playerHealth, playerLevel, maxHealthScale
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, dogMessage
    callRemote shellHandle, displayMessageIndex
    jmp enemyAttack
    
    LBL handleStalactite
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?stalactiteMessageStart, ?stalactiteMessageEnd
    addTextToString stringBuilder, enemySpeciesName
    addCharToString stringBuilder, 33
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    wrt damageAmount, 50
    jmp damageEnemy
    
    LBL enemyAttack
    jmpNG cleanUp, enemyHealth, 0
    createStringBuilder stringBuilder
    addTextToString stringBuilder, enemySpeciesName
    addAppDataToString stringBuilder, ?usedMessageStart, ?usedMessageEnd
    addTextToString stringBuilder, enemyAttackName
    addCharToString stringBuilder, 33
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    newArgFrame generateRandomInt.argsSize
    wrt generateRandomInt.args.maximumValue, 3
    callRemote utilsHandle, randomIntIndex
    add damageAmount, enemyLevel, generateRandomInt.args.dest
    sub playerHealth, prevArg, damageAmount
    jmpNG skipClampPlayer, 0, playerHealth
    wrt playerHealth, 0
    LBL skipClampPlayer
    
    createStringBuilder stringBuilder
    addAppDataToString stringBuilder, ?youMessageStart, ?lostMessageEnd
    addIntToString stringBuilder, damageAmount
    addAppDataToString stringBuilder, ?healthMessageStart, ?healthMessageEnd
    finishString message, stringBuilder
    newArgFrame wsDisplayWindowMessage.argsSize
    wrt wsDisplayWindowMessage.args.window, window
    wrt wsDisplayWindowMessage.args.message, message
    callRemote shellHandle, displayMessageIndex
    delAlloc message
    
    LBL cleanUp
    delAlloc promptMessage
END

FUNC windowKeyPressed, wsWindowKeyPressed.id, guarded
    ARG window, s32
    ARG key, s8
    
    VAR finishSeedIndex, s32
    findFunc finishSeedIndex, utilsHandle, finishRandomSeed.id
    newArgFrame finishRandomSeed.argsSize
    wrt finishRandomSeed.args.key, key
    callRemote utilsHandle, finishSeedIndex
END

FUNC cleanUpAndQuit, 0, guarded
    VAR delWindowIndex, s32
    findFunc delWindowIndex, shellHandle, wsDelWindow.id
    newArgFrame wsDelWindow.argsSize
    wrt wsDelWindow.args.window, window
    callRemote shellHandle, delWindowIndex
    closeFile shellHandle
    closeFile utilsHandle
    quitApp
END

FUNC kill, wsKill.id, guarded
    call cleanUpAndQuit
END

FUNC reqDelWindow, wsReqDelWindow.id, guarded
    call cleanUpAndQuit
END

APP_DATA
    LBL shellNameStart
    DATA "wsShell"
    LBL shellNameEnd
    LBL utilsNameStart
    DATA "utils"
    LBL utilsNameEnd
    LBL enemyTableStart
    DATA ?spiderNameStart:s32, ?spiderNameEnd:s32
    DATA ?spiderAttackStart:s32, ?spiderAttackEnd:s32
    DATA ?snakeNameStart:s32, ?snakeNameEnd:s32
    DATA ?snakeAttackStart:s32, ?snakeAttackEnd:s32
    DATA ?batNameStart:s32, ?batNameEnd:s32
    DATA ?batAttackStart:s32, ?batAttackEnd:s32
    DATA ?bearNameStart:s32, ?bearNameEnd:s32
    DATA ?bearAttackStart:s32, ?bearAttackEnd:s32
    LBL enemyTableEnd
    LBL spiderNameStart
    DATA "Spider"
    LBL spiderNameEnd
    LBL spiderAttackStart
    DATA "Venom"
    LBL spiderAttackEnd
    LBL snakeNameStart
    DATA "Snake"
    LBL snakeNameEnd
    LBL snakeAttackStart
    DATA "Tail Whip"
    LBL snakeAttackEnd
    LBL batNameStart
    DATA "Bat"
    LBL batNameEnd
    LBL batAttackStart
    DATA "Shriek"
    LBL batAttackEnd
    LBL bearNameStart
    DATA "Bear"
    LBL bearNameEnd
    LBL bearAttackStart
    DATA "Scratch"
    LBL bearAttackEnd
    LBL introMessageStart
    DATA "Before you lies the mouth of a spooky cave. Legend has it that the cave contains the riches of Old Man Herman. Are you tough enough to reach the treasure?"
    LBL introMessageEnd
    LBL cowardMessageStart
    DATA "You chicken out and decide that Old Man Herman's treasure is not worth the risk. (Bad end)"
    LBL cowardMessageEnd
    LBL deathMessageStart
    DATA "You have come to an untimely demise. The treasure of Old Man Herman remains undiscovered."
    LBL deathMessageEnd
    LBL victoryMessageStart
    DATA "At the farthest depths of the cave, you spot the treasure of Old Man Herman. Brushing off the dust, you find that it is a jar of fancy olives. YOU WIN! (Good end)"
    LBL victoryMessageEnd
    LBL levelLabelStart
    DATA "LV "
    LBL levelLabelEnd
    LBL playerNameStart
    DATA " Player"
    LBL playerNameEnd
    LBL posLabelStart
    DATA "Dist="
    LBL posLabelEnd
    LBL healthLabelStart
    DATA "HP="
    LBL healthLabelEnd
    LBL energyLabelStart
    DATA "EP="
    LBL energyLabelEnd
    LBL experienceLabelStart
    DATA "XP="
    LBL experienceLabelEnd
    LBL restOptionStart
    DATA "Rest"
    LBL restOptionEnd
    LBL forwardOptionStart
    DATA "Travel deeper"
    LBL forwardOptionEnd
    LBL backwardOptionStart
    DATA "Retreat"
    LBL backwardOptionEnd
    LBL restMessageStart
    DATA "You took a nap, and regained energy."
    LBL restMessageEnd
    LBL wildMessageStart
    DATA "A wild "
    LBL wildMessageEnd
    LBL appearedMessageStart
    DATA " appeared!"
    LBL appearedMessageEnd
    LBL punchOptionStart
    DATA "Punch (0 EP)"
    LBL punchOptionEnd
    LBL kickboxOptionStart
    DATA "Kickbox (2 EP)"
    LBL kickboxOptionEnd
    LBL helpOptionStart
    DATA "Call for help (0 EP)"
    LBL helpOptionEnd
    LBL defeatEnemyMessageStart
    DATA "You defeated "
    LBL defeatEnemyMessageEnd
    LBL earnedMessageStart
    DATA "You earned "
    LBL earnedMessageEnd
    LBL experienceMessageStart
    DATA " XP!"
    LBL experienceMessageEnd
    LBL levelUpMessageStart
    DATA "You reached level "
    LBL levelUpMessageEnd
    LBL youMessageStart
    DATA "You"
    LBL youMessageEnd
    # Note that youMessage and lostMessage may sometimes be referenced together.
    LBL lostMessageStart
    DATA " lost "
    LBL lostMessageEnd
    LBL healthMessageStart
    DATA " HP!"
    LBL healthMessageEnd
    LBL usedMessageStart
    DATA " used "
    LBL usedMessageEnd
    LBL lowEnergyMessageStart
    DATA "You don't have enough energy!"
    LBL lowEnergyMessageEnd
    LBL helpMessageStart
    DATA "You shout for help..."
    LBL helpMessageEnd
    LBL helplessMessageStart
    DATA "...but nothing happened."
    LBL helplessMessageEnd
    LBL catMessageStart
    DATA "A cat brings you a protein bar. Your EP is restored!"
    LBL catMessageEnd
    LBL dogMessageStart
    DATA "A dog brings you a first aid kit. Your HP is restored!"
    LBL dogMessageEnd
    LBL stalactiteMessageStart
    DATA "A stalactite falls on the "
    LBL stalactiteMessageEnd
END


